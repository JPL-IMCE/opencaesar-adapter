buildscript {
	repositories {
	    mavenLocal()
  		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:2.0.8'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

subprojects {
	group = 'io.opencaesar.legacy'
	version = '0.7.4'

	ext.versions = [
		legacy: '0.9.7.+',
		oml: '0.7.+',
	]

	repositories {
	    mavenLocal()
	    maven { url 'https://dl.bintray.com/jpl-imce/gov.nasa.jpl.imce.oml' }
	    maven { url 'https://dl.bintray.com/opencaesar/oml' }
		mavenCentral()
		jcenter()
	}
	
	apply plugin: 'java-library'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'org.xtext.xtend'
	
	apply from: "${rootDir}/gradle/source-layout.gradle"
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
	
	sourceCompatibility = '11'
	targetCompatibility = '11'
	
	task createVersion {
	    doLast {
	        def version = project.version.toString()
	        def file = new File("$projectDir/src/main/resources/version.txt")
	        file.write(version)
	    }
	}
	
	classes {
	    dependsOn createVersion
	}
}